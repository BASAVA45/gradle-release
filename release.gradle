apply plugin: 'groovy'
apply from: 'installation/apply-local.groovy'

configurations {
	installers {}
}

repositories {
	mavenCentral()
	mavenRepo url: 'http://m2repo.spockframework.org/snapshots'
}

dependencies {
	compile gradleApi()
	groovy localGroovy()
	testCompile( 'org.spockframework:spock-core:0.5-groovy-1.8' ) { exclude group: 'org.codehaus.groovy' }
	testCompile 'junit:junit:4.10'
}

// This updates the apply script with the new version.
updateVersion << {
	def applyFile = new File("installation", "apply.groovy")
	def readmeFile = new File("README.md")
	def oldVersion = project.getProperty("release.oldVersion")
	def newVersion = project.getProperty("release.newVersion")
	ant.replace(file: applyFile, token: "${group}:${name}:${oldVersion}", value: "${group}:${name}:${newVersion}")
	ant.replaceregexp(file: readmeFile, match: "Current Version: .*?\$", replace: "Current Version: ${oldVersion}", byline: true)
	ant.replaceregexp(file: readmeFile, match: "gradle-release/\\d+\\.\\d+/apply.groovy", replace: "gradle-release/${oldVersion}/apply.groovy", byline: true)
}

artifacts {
	archives jar
	installers file('installation/apply.groovy')
}

uploadArchives {
	repositories {
		add(new org.apache.ivy.plugins.resolver.SFTPResolver()) {
			host = releaseRepoHost
			addIvyPattern "${releaseRepoBase}/[module]/[revision]/[artifact]-[revision].[ext]"
			addArtifactPattern "${releaseRepoBase}/[module]/[revision]/[artifact]-[revision].[ext]"
		}
	}
}

uploadInstallers {
	repositories {
		add(new org.apache.ivy.plugins.resolver.SFTPResolver()) {
			host = releaseRepoHost
			addArtifactPattern "${releaseRepoBase}/[module]/[revision]/[artifact].[ext]"
		}
		add(new org.apache.ivy.plugins.resolver.SFTPResolver()) {
			host = releaseRepoHost
			addArtifactPattern "${releaseRepoBase}/[module]/[artifact].[ext]"
		}
	}
}

createReleaseTag.dependsOn uploadArchives
createReleaseTag.dependsOn uploadInstallers