apply plugin: 'groovy'
apply plugin: 'idea'

// Run "gradle installPlugin" first
// apply plugin: release.ReleasePlugin

codenarcRuleSetFiles = [ 'codenarc.groovy' ]
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/src/main/groovy/CodeNarc.gradle'


group = 'gradle-release'

defaultTasks 'clean', 'build'


dependencies {
   compile gradleApi(),
           'org.gcontracts:gcontracts-core:1.2.4'
   groovy  localGroovy()
}


task( 'unversionedJar', type: Jar, dependsOn: 'jar' ) {
    version = null
    from sourceSets.main.classes
}

def installedJar = file( "${gradle.gradleHomeDir.canonicalPath}/lib/plugins/${unversionedJar.archiveName}" )

task( 'installPlugin', type: Copy, dependsOn: 'unversionedJar' ) {
    group       = 'installation'
    description = 'Installs the plugin jar in your gradle distribution.'
    from unversionedJar.archivePath
    into installedJar.parentFile
    doLast {
        assert installedJar.file
        println "Plugin installed as [$installedJar]"
    }
}

task( 'uninstallPlugin', type: Delete ) {
    group       = 'installation'
    description = 'Removes the plugin from your gradle distribution.'
    delete ( installedJar )
    doLast {
        assert ! installedJar.file
        println "[$installedJar] deleted"
    }
}
