buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
        classpath 'org.codehaus.groovy:groovy-backports-compat23:2.4.0'
        classpath 'net.researchgate:gradle-release:2.0.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nu.studer.plugindev'
apply plugin: 'net.researchgate.release'

defaultTasks 'clean', 'build'

group='net.researchgate'

repositories {
	maven { url "https://oss.sonatype.org/content/groups/public"} // for spock SNAPSHOTs
}

dependencies {
	testCompile("org.spockframework:spock-core:$spockVersion") { exclude group: 'org.codehaus.groovy' }
	testCompile "junit:junit:$junitVersion"
	testCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
}

plugindev {
	pluginImplementationClass 'net.researchgate.release.ReleasePlugin'
	pluginDescription 'gradle-release is a plugin for providing a Maven-like release process to project using Gradle.'
	pluginLicenses 'MIT'
	pluginTags 'gradle', 'plugin', 'release'
	authorId 'danez'
	authorName 'Daniel Tschinder'
	authorEmail 'daniel.tschinder@researchgate.net'
	projectUrl 'https://github.com/researchgate/gradle-release'
	projectInceptionYear '2011'
	done() // do not omit this
}

bintray {
	user = project.hasProperty('bintrayUser') ? bintrayUser : ''
	key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
	pkg {
		repo = 'gradle-plugins'
		userOrg = 'researchgate'
		version {
			gpg {
				sign = true
				passphrase = project.hasProperty('bintrayGpgPassphrase') ? bintrayGpgPassphrase : ''
			}
		}
	}
    publish = true
}

createReleaseTag.dependsOn publishPluginToBintray
publishPluginToBintray.dependsOn preTagCommit

wrapper.gradleVersion = '1.12'
